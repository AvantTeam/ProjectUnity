buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.github.Anuken.Arc:arc-core:$arcVersion"
        classpath "com.github.Anuken.Mindustry:core:$mindustryVersion"
        classpath "com.squareup:javapoet:$javapoetVersion"
        classpath "com.github.Anuken:jabel:$jabelVersion"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'com.github.AvantTeam'
    version = '0.1'

    ext {
        sdkVersion = { return '30' }
        sdkAPI = { return '14' }
        sdkRoot = { return System.getenv('ANDROID_SDK_ROOT') }
        hasSprites = { return new File(rootDir, 'main/assets/sprites/').exists() }
        windows = { return System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows') }
        devBuild = { return project.hasProperty('dev') && Boolean.parseBoolean(project.property('dev') as String) }
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += '-Xlint:deprecation'
    }

    tasks.withType(JavaExec) {
        jvmArgs += "--enable-preview"
    }
}

configure(project(':annotations')) {
    sourceCompatibility = 8
    targetCompatibility = 8

    tasks.withType(JavaCompile) {
        targetCompatibility = 8
        sourceCompatibility = 8

        options.incremental = true
    }
}

configure(subprojects - project(':annotations')) {
    sourceCompatibility = 15
    targetCompatibility = 8

    tasks.withType(JavaCompile) {
        sourceCompatibility = 15
        targetCompatibility = 8

        options.incremental = true
        options.encoding 'UTF-8'
        if(JavaVersion.current() != JavaVersion.VERSION_1_8) {
            options.compilerArgs.addAll(['--release', '8', '--enable-preview'])
        }

        doFirst {
            options.compilerArgs = options.compilerArgs.findAll { it != '--enable-preview' }
        }
    }
}
