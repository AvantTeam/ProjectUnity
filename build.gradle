buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.github.Anuken.Arc:arc-core:$mindustryVersion"
        classpath "com.github.Anuken.Mindustry:core:$mindustryVersion"
        classpath "com.squareup:javapoet:$javapoetVersion"
        classpath "com.github.Anuken:jabel:$jabelVersion"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    group = 'com.github.AvantTeam'
    version = '0.1'

    ext {
        sdkVersion = { return '30' }
        sdkAPI = { return '14' }
        sdkRoot = { return System.getenv('ANDROID_SDK_ROOT') }
        hasSprites = { return new File(rootDir, 'main/assets/sprites/').exists() }
        windows = { return System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows') }
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

configure(project(':annotations')) {
    sourceCompatibility = 8
    targetCompatibility = 8

    tasks.withType(JavaCompile).all {
        targetCompatibility = 8
        sourceCompatibility = 8
    }
}

configure(subprojects - project(':annotations')) {
    sourceCompatibility = 15
    targetCompatibility = 8

    tasks.withType(JavaCompile).all {
        sourceCompatibility = 15
        targetCompatibility = 8

        options.encoding 'UTF-8'
        if(JavaVersion.current() != JavaVersion.VERSION_1_8) {
            options.compilerArgs.addAll(['--release', '8', '--enable-preview'])
        }

        doFirst {
            options.compilerArgs = options.compilerArgs.findAll { it != '--enable-preview' }
        }
    }

    tasks.withType(JavaExec).all {
        jvmArgs += "--enable-preview"
    }
}
