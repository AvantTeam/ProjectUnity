buildscript{
    ext{
        props = new Properties().with{ p -> p.load(file("gradle.properties").newReader()); return p }

        mindustryVersion = props["mindustryVersion"]
        javapoetVersion = props["javapoetVersion"]
        multilibVersion = props["multilibVersion"]
        jabelVersion = props["jabelVersion"]
    }

    repositories{
        mavenCentral()
        maven{ url "https://jitpack.io" }
    }

    dependencies{
        classpath "com.github.Anuken.Arc:arc-core:$mindustryVersion"
        classpath "com.github.Anuken.Mindustry:core:$mindustryVersion"
        classpath "com.squareup:javapoet:$javapoetVersion"
        classpath "com.github.younggam:multi-lib:$multilibVersion"
        classpath "com.github.Anuken:jabel:$jabelVersion"
    }
}

allprojects{
    apply plugin: "java"
    apply plugin: "java-library"

    group = "com.github.avant"
    version = "0.4"
    targetCompatibility = 8
    sourceCompatibility = 15

    ext{
        mindustryVersion = props["mindustryVersion"]
        javapoetVersion = props["javapoetVersion"]
        multilibVersion = props["multilibVersion"]
        jabelVersion = props["jabelVersion"]

        modName = props["modName"]

        sdkVersion = "30"
        sdkRoot = System.getenv("ANDROID_HOME")
    }

    repositories{
        mavenCentral()
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url "https://oss.sonatype.org/content/repositories/releases/" }
        maven{ url "https://jitpack.io" }
        jcenter()
    }

    tasks.withType(JavaCompile){
        options.encoding("UTF-8")
        if(JavaVersion.current() != JavaVersion.VERSION_1_8){
            options.compilerArgs.addAll(['--release', '8', '--enable-preview'])
        }

        doFirst{
            options.compilerArgs = options.compilerArgs.findAll{ it != '--enable-preview' }
        }
    }
}
