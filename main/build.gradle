sourceSets.main.java.srcDirs = ["src/", "$buildDir/generated/sources/annotationProcessor/java/main/"]
sourceSets.main.resources.srcDirs = ["assets/"]

repositories{
    mavenCentral()
    maven{url "https://www.jitpack.io"}
}

ext{
    sdkRoot = System.getenv("ANDROID_HOME")
    sdkVersion = "30.0.1"

    doExec = {cmd ->
        def proc = cmd.execute(null, new File("$buildDir/libs"))
        proc.waitForProcessOutput(System.out, System.err)
    }
}

task jarAndroid(dependsOn: jar){
    doLast{
        def files = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File("$sdkRoot/platforms/android-$sdkVersion/android.jar")])
        def dependencies = files.collect{"--classpath $it.path"}.join(" ")

        doExec("dx $dependencies --min-api 14 --output ${modName}Android.jar ${modName}Desktop.jar")
    }
}

jar{
    archiveFileName = "${modName}Desktop.jar"

    from{
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }
}

task deploy(type: Jar, dependsOn: [jarAndroid, jar]){
    archiveFileName = "${modName}.jar"

    from{[zipTree("$buildDir/libs/${modName}Desktop.jar"), zipTree("$buildDir/libs/${modName}Android.jar")]}

    doLast{
        delete{
            delete "$buildDir/libs/${modName}Desktop.jar"
            delete "$buildDir/libs/${modName}Android.jar"
        }
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"

    implementation "com.github.younggam:multi-lib:$multilibVersion"
    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"

    compileOnly project(":annotations")
    annotationProcessor project(":annotations")

    compileJava.options.compilerArgs.addAll([
        "-processor", "unity.annotations.FactionProcessor,unity.annotations.ExpProcessor,unity.annotations.EntityProcessor"
    ])
}
