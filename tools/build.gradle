sourceSets.main.java.srcDirs = ['src/']

ext {
    imageProcessorMain = 'unity.tools.SpriteProcessor'

    spritesGenFolder = '../main/assets-raw/sprites-gen/'
    spritesAssetsFolder = '../main/assets/sprites/'
}

task proc(dependsOn: [classes, configurations.runtimeClasspath]) {
    doLast {
        delete {
            delete spritesGenFolder
            delete spritesAssetsFolder
        }

        file(spritesGenFolder).mkdirs()
        file(spritesAssetsFolder).mkdirs()

        javaexec {
            main imageProcessorMain
            classpath sourceSets.main.runtimeClasspath
            jvmArgs += '-Djava.awt.headless=true'
            standardInput System.in
            workingDir file('../main/assets-raw/')
        }

        copy {
            from spritesGenFolder
            into spritesAssetsFolder
        }

        exec {
            ignoreExitValue true
            workingDir file('../main/')

            println()
            println 'Alpha-bleeding sprites...'
            if(windows()) {
                commandLine('cmd', '/c', 'alphableed.bat', './assets/sprites/')
            } else {
                commandLine('./alpha-bleed', './assets/sprites/')
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    implementation project(':main')

    implementation "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    implementation "com.github.Anuken.Mindustry:core:$mindustryVersion"
}
